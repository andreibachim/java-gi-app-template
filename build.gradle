plugins {
    id "application"
    id "io.github.jwharm.flatpak-gradle-generator" version "1.5.0"
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url = "build/repository" } // used by flatpak-builder
}

dependencies {
    implementation "io.github.jwharm.javagi:adw:0.13.0-RC1"
}

// Task to compile gresource files
tasks.register("compileResources") {
    exec {
        workingDir "src/main/resources"
        commandLine "glib-compile-resources", "example.gresource.xml"
    }
}

// Generate a file with all dependency urls for the offline flatpak build
tasks.flatpakGradleGenerator {
    outputFile.set(file("$rootDir/data/maven-dependencies.json"))
    downloadDirectory.set("build/repository")
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(23)
    }
}

tasks.named("compileJava") {
    dependsOn compileResources
}

tasks.named('installDist') {
    destinationDir = file('/app')
}

application {
    applicationName = "example"
    mainClass = "org.domain.Example"
    applicationDefaultJvmArgs += "--enable-native-access=ALL-UNNAMED"
}

